//SPWN Script that converts the Mandelbrot fractal to a Geometry Dash level, made by Cosh_X
//All the pixels are colored after the number of iterations that z takes to diverge past the value 2
//Every pixel also is in group 999 if you want to add any extra effects

extract obj_props
for n in 0..99 {
    let x = 1 + n
    for k in 0..99 {
        let y = 1 + k
        let c = @complex::new((-2 + (2.4 / 98) * n), (1.2 - (2.4 / 98) * k))
        let z = @complex::new(0)
        let pixel = obj {
            OBJ_ID: 279,
            X: 15 * x,
            Y: 1500 - 15 * y,
            COLOR: 1c,
            SCALING: 0.5,
            HVS_ENABLED: true,
            GROUPS: 999g,
            }
        for i in 1..12 {
            if z.abs() > 2 {       
                $.add(pixel.with(GROUPS, [(i as @group), 999g]))
                break
            }
            if i == 11{
                $.add(pixel.with(HVS, "60a0a0a0a0"))
                break
            }
            z = z ^ 2 + c
        }
    }
}
for h in 1..12 {
    ->(h as @group).pulse((-180 + (360 / 11) * h), 1, 1, hold = 999, hsv = true)
}
